resource "oci_core_instance_configuration" "leader_instance_configuration" {
  compartment_id = oci_identity_compartment.tf_compartment.id
  display_name   = "leader-pool-configuration"

  instance_details {
    instance_type = "compute"

    launch_details {
      compartment_id = oci_identity_compartment.tf_compartment.id

      display_name = "leader"

      shape = "VM.Standard.A1.Flex"
      shape_config {
        ocpus         = 1
        memory_in_gbs = 6
      }
      source_details {
        source_type = "image"
        image_id    = data.oci_core_images.ubuntu_arm.images[0].id
      }

      create_vnic_details {
        assign_public_ip = true
        subnet_id        = oci_core_subnet.vcn_public_subnet.id
      }

      metadata = {
        ssh_authorized_keys = file(var.leader_ssh_key_pub)
      }
    }
  }
}

resource "oci_core_instance_pool" "leader_instance_pool" {
  compartment_id            = oci_identity_compartment.tf_compartment.id
  instance_configuration_id = oci_core_instance_configuration.leader_instance_configuration.id
  size                      = 1
  display_name              = "leader-pool"

  placement_configurations {
    availability_domain = data.oci_identity_availability_domains.ads.availability_domains[0].name
    primary_subnet_id   = oci_core_subnet.vcn_public_subnet.id
  }
}
